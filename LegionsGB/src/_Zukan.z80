;------------
;	Zukan
;	図鑑画面の処理
;	by yuyat

;---------------------------------------------------------------------------
;図鑑用グラフィックパターン定義
;図鑑画面で使う共通グラフィックパターンを定義する
;---------------------------------------------------------------------------
b_SetZukan:
	call	ScreenOff
	;VRAMバンクを0に
	xor	a
	ldh	[rVBK],a
	;図鑑用グラフィック読み込み
	ld	hl,g_Zukan
	ld	de,_VRAM + $1000
	ld	bc,48 * 16
	call	LDIR
	;カードグラフィック読み込み
	ld	hl,g_cg123
	ld	de,_VRAM + $1280
	ld	bc,72 * 16
	call	LDIR
	;枠マップ定義
	ld	b,32
	ld	c,13
	ld	de,ZukanMap
	ld	hl,_SCRN0
	call	g_SetTileMap
	;枠マップ定義（ウィンドウ面）
	ld	b,20
	ld	c,5
	ld	de,ZukanWinMap
	ld	hl,_SCRN1
	call	g_SetTileMap
	;カードマップ定義
	ld	de,cg123Map
	ld	hl,_SCRN0 + 34
	call	g_SetCardMap
	;ミニキャラマップ定義
	ld	de,mg123Map
	ld	hl,_SCRN0 + 46
	call	g_SetCharaMap

	;VRAMバンクを1に
	ld	a,1
	ldh	[rVBK],a				; VRAMバンク 1
	;ミニキャラグラフィック読み込み
	ld	hl,g_mg123
	ld	de,_VRAM + $800
	ld	bc,9 * 16
	call	LDIR
	;名前グラフィック読み込み
	ld	hl,g_Name
	ld	de,_VRAM + $A00
	ld	bc,32 * 16
	call	LDIR
	;テキストグラフィック読み込み
	ld	hl,g_Text
	ld	de,_VRAM + $1000
	ld	bc,128 * 16
	call	LDIR
	;枠マップアトリビュート定義
	ld	b,32
	ld	c,13
	ld	de,ZukanColMap
	ld	hl,_SCRN0
	call	g_SetTileMap
	;枠マップアトリビュート定義（ウィンドウ面）
	ld	b,20
	ld	c,5
	ld	de,ZukanWinColMap
	ld	hl,_SCRN1
	call	g_SetTileMap
	;カードアトリビュート定義
	ld	b,8
	ld	c,11
	ld	de,cg123ColMap
	ld	hl,_SCRN0 + 34
	call	g_SetTileMap
	;ミニキャラアトリビュート定義
	ld	b,4
	ld	c,4
	ld	de,mg123ColMap
	ld	hl,_SCRN0 + 46
	call	g_SetTileMap

	;VRAMバンクを0に
	xor	a
	ldh	[rVBK],a
	;カードカラー定義
	ld	d,32
	ld	e,0
	ld	hl,cg123Pal
	call g_SetBGPallet
	;属性カラー定義
	ld	d,8
	ld	e,32
	ld	hl,ElementPal + 8
	call g_SetBGPallet
	ld	d,8
	ld	e,40
	ld	hl,ElementPal + 40
	call g_SetBGPallet

	;ウィンドウ面表示
	ld	a,104
	ldh	[rWY],a					;Window Y 表示
	ld	a,7
	ldh	[rWX],a					;Window X 表示

	;スクロール位置の指定
	xor	a
	ld	[z_Position],a
	;右矢印のスプライトの指定
	ld	a,64
	ld	[OAM_BUFFER0],a
	ld	a,160
	ld	[OAM_BUFFER0 + 1],a
	ld	a,1
	ld	[OAM_BUFFER0 + 2],a
	ld	a,$07
	ld	[OAM_BUFFER0 + 3],a
	;画面のスクロール
	ld	a,[z_Position]
	ldh	[rSCX],a

	call	ScreenOnVB
	SetScene	b_Zukan
	ret

b_Zukan:
	;ボタン入力で表示切り替えを行う
	ld	a,[_PadDebounce]
	bit	4,a
	jp	nz,.right
	bit	5,a
	jp	nz,.left
	jp	.endif
.right:
	;スクロール位置の指定
	ld	a,96
	ld	[z_Position],a
	;左矢印のスプライトの指定
	ld	a,64
	ld	[OAM_BUFFER0],a
	ld	a,8
	ld	[OAM_BUFFER0 + 1],a
	ld	a,1
	ld	[OAM_BUFFER0 + 2],a
	ld	a,$27
	ld	[OAM_BUFFER0 + 3],a
	jp	.endif
.left:
	;スクロール位置の指定
	xor	a
	ld	[z_Position],a
	;右矢印のスプライトの指定
	ld	a,64
	ld	[OAM_BUFFER0],a
	ld	a,160
	ld	[OAM_BUFFER0 + 1],a
	ld	a,1
	ld	[OAM_BUFFER0 + 2],a
	ld	a,$07
	ld	[OAM_BUFFER0 + 3],a
.endif:
	;画面のスクロール
	ld	a,[z_Position]
	ldh	[rSCX],a
	ret