;------------
;	FieldMenu
;	メニュー画面の処理
;	by yuyat

;手札画面初期化
bf_SetHandMenu:
	ld	a, [f_MainPhaseScene]
	cp	$F0
	jr	z,.skip
	;フィールド画面からではない場合、フィールド初期化を呼び出す
	call	bf_SetField
	ld	a,$F0
	ld	[f_MainPhaseScene], a
.skip:
	;メニュー画面を表示する
	ld	b,20
	ld	c,2
	ld	de,StatusMap
	ld	hl,MENUMAP_BUFFER
	call	g_SetTileMap

	ld	b,20
	ld	c,6
	ld	de,HandMap
	ld	hl,MENUMAP_BUFFER + 64
	call	g_SetTileMap
	
	;パラメーター設定
	;自分の手札枚数表示
	ld	a,[b_cHandCount]
	ld	b,a
	ld	a,[b_cHandCount + 1]
	ld	c,a
	ld	a,[bc]
	ld	d,a
	ld	e,$D0
	ld	hl,MENUMAP_BUFFER + 7
	call	g_Set2BCDBG
	;自分のHP表示
	ld	a,[b_cFieldUnit]
	ld	b,a
	ld	a,[b_cFieldUnit + 1]
	add	a,4
	ld	c,a
	ld	a,[bc]
	ld	d,a
	ld	e,$D0
	ld	hl,MENUMAP_BUFFER + 33
	call	g_Set2BCDBG

	;相手の手札枚数表示
	ld	a,[b_oHandCount]
	ld	b,a
	ld	a,[b_oHandCount + 1]
	ld	c,a
	ld	a,[bc]
	ld	d,a
	ld	e,$D0
	ld	hl,MENUMAP_BUFFER + 17
	call	g_Set2BCDBG
	;相手のHP表示
	ld	a,[b_oFieldUnit]
	ld	b,a
	ld	a,[b_oFieldUnit + 1]
	add	a,4
	ld	c,a
	ld	a,[bc]
	ld	d,a
	ld	e,$D0
	ld	hl,MENUMAP_BUFFER + 44
	call	g_Set2BCDBG

	;メニュー画面をアトリビュートを指定する
	ld	b,20
	ld	c,2
	ld	de,StatusColMap
	ld	hl,MENUATRMAP_BUFFER
	call	g_SetTileMap
	
	ld	b,20
	ld	c,6
	ld	de,HandColMap
	ld	hl,MENUATRMAP_BUFFER + 64
	call	g_SetTileMap
	
	;ウィンドウ面表示
	ld	a,80
	ldh	[rWY],a					;Window Y 表示
	ld	a,7
	ldh	[rWX],a					;Window X 表示
	
	;手札画面に移行する
	SetScene	bf_HandMenu
	ret

;手札画面
bf_HandMenu:
	;操作
	;ボタン入力で選択を行う
	ld	a,[_PadDebounce]
	ld	b,a
	bit	4,a
	jp	nz,.right
	bit	5,a
	jp	nz,.left
	jp	.endiflr
.right:
	;選択位置-1
	ld	a,[f_HandPosition]
	inc	a
	ld	[f_HandPosition],a
	jp	.endiflr
.left:
	;選択位置+1
	ld	a,[f_HandPosition]
	dec	a
	ld	[f_HandPosition],a
.endiflr:
	;コマンドメニューへ移動
	ld	a,b
	bit	0,a
	jp	z,.endifa
	;選択項目番号初期化
	xor	a
	ld	[f_SelectCommand],a
	SetScene	bf_HandCardMenu
.endifa:
	;処理
	;現在選んでいるカードデータの読み込み・定義
	ld	a,[f_HandPosition]	;手札位置をレジスタeに格納
	ld	e,a
	ld	a,[b_cHand + 1]		;手札配列を元にSelectCardにカード種を読み込む
	add	a,e
	add	a,e
	ld	c,a
	ld	a,[b_cHand]
	adc	a,0
	ld	b,a
	ld	a,[bc]			;今のループの値分ずらす
	ld	[f_SelectCard],a
	inc	bc
	ld	a,[bc]
	ld	[f_SelectCard + 1],a
	;描画
	;下地の表示
	ld	b,20
	ld	c,4
	ld	de,HandMap
	ld	hl,MENUMAP_BUFFER + 64
	call	g_SetTileMap
	ld	b,20
	ld	c,4
	ld	de,HandColMap
	ld	hl,MENUATRMAP_BUFFER + 64
	call	g_SetTileMap
	;手札の表示
	;ループ用変数の初期化
	ld	a,[b_cHandCount]	;手札枚数をdに代入
	ld	h,a
	ld	a,[b_cHandCount + 1]
	ld	l,a
	ld	a,[hl]
	ld	d,a
	ld	e,0
	;手札の描画
.loop
	push	de			;場所の定義
	ld	hl,MENUMAP_BUFFER + 98
	ld	a,[f_HandPosition]
	cp	e
	jp	nz,.neq
	ld	a,-32
	jp	.endif
.neq
	xor	a
.endif
	add	a,l
	add	a,e			;横にずらす
	add	a,e
	ld	l,a
	;カードの種類をbcに代入
	ld	a,[b_cHand + 1]
	add	a,e
	add	a,e
	ld	c,a
	ld	a,[b_cHand]
	adc	a,0
	ld	b,a
	ld	a,[bc]			;今のループの値分ずらす
	ld	d,a
	inc	bc
	ld	a,[bc]
	ld	e,a
	call	g_SetMiniCardMap
	pop	de
	inc	e
	dec	d
	jp	nz,.loop
	;ステータスの表示
	;カードデータの読み込み
	ld	a,[f_SelectCard]			;バンク切り替え
	ld	[rROMB0],a
	ld	a,[f_SelectCard + 1]
	add	a,64
	ld	h,a
	ld	a,42
	ld	l,a
	;コストの表示(42番目)
	ld	a,[hl+]
	push	hl
	ld	d,a
	ld	e,$D0
	ld	hl,MENUMAP_BUFFER + 229
	call	g_Set2BCDBG
	pop	hl
	;WT/CCの表示(43番目)
	ld	a,[hl+]
	push	hl
	ld	hl,MENUMAP_BUFFER + 232
	add	a,$D0
	ld	[hl],a
	pop	hl
	;ATKの表示(44番目)
	ld	a,[hl+]
	push	hl
	ld	d,a
	ld	e,$D0
	ld	hl,MENUMAP_BUFFER + 234
	call	g_Set2BCDBG
	pop	hl
	;HPの表示(45番目)
	ld	a,[hl]
	push	hl
	ld	d,a
	ld	e,$D0
	ld	hl,MENUMAP_BUFFER + 237
	call	g_Set2BCDBG
	pop	hl
	ld	a,1			;バンク切り替え
	ld	[rROMB0],a
	;フィールド画面の描画
	call	bf_Field
	ret

;手札カードメニュー画面
bf_HandCardMenu:
	;操作
	;ボタン入力で選択を行う
	ld	a,[_PadDebounce]
	ld	b,a
	bit	4,a
	jp	nz,.right
	bit	5,a
	jp	nz,.left
	jp	.endiflr
.right:
	;選択位置-1
	ld	a,[f_SelectCommand]
	inc	a
	ld	[f_SelectCommand],a
	jp	.endiflr
.left:
	;選択位置+1
	ld	a,[f_SelectCommand]
	dec	a
	ld	[f_SelectCommand],a
.endiflr:
	;コマンドの実行
	ld	a,b
	bit	0,a
	jp	nz,.a
	bit	1,a
	jp	nz,.b
	jp	.endifab
.a:
	;Aボタンで選択内容を実行
	call	DisCard
	ld	a,[f_SelectCommand]	;「プレイ」コマンドが実行された時、ユニットの配置を行う
	cp	0
	call	z,UnitSet
	;もし自ユニットが配置されているか判定する
	ld	a,[b_cFieldUnit + 1]	;中央ミニオンの場所に指定する
	add	240
	ld	l,a
	ld	a,[b_cFieldUnit]
	adc	a,0
	ld	h,a
	;カード番号を読み込む
	ld	a,[hl]
	cp	0
	jp	z,.endphase		;もしフィールドにカードデータが存在しない場合、戦闘を行わない
	;ユニットが存在する場合、戦闘を行う
	SetScene	bf_SetBattleSelect
	jp	.endifab
.endphase
	SetScene	bf_SetEndPhase
	jp	.endifab
.b:
	;Bボタンで手札メニューに戻る
	SetScene	bf_SetHandMenu
.endifab:
	;表示
	;固定枠表示
	ld	b,2
	ld	c,2
	ld	de,CommandMap
	ld	hl,MENUMAP_BUFFER + 10
	call	g_SetTileMap
	;フィールド配置アイコン
	ld	b,2
	ld	c,2
	ld	de,PrayIconMap
	ld	hl,MENUMAP_BUFFER + 12
	call	g_SetTileMap
	;スタンバイ配置アイコン
	ld	b,2
	ld	c,2
	ld	de,SetIconMap
	ld	hl,MENUMAP_BUFFER + 14
	call	g_SetTileMap
	;白塗り
	ld	b,4
	ld	c,2
	ld	d,$00
	ld	hl,MENUMAP_BUFFER + 16
	call	g_SetTileFill
	;パレット定義
	ld	b,2
	ld	c,2
	ld	d,$06
	ld	hl,MENUATRMAP_BUFFER + 10
	call	g_SetTileFill
	ld	b,8
	ld	c,2
	ld	d,$05
	ld	hl,MENUATRMAP_BUFFER + 12
	call	g_SetTileFill
	;選択したアイコンのパレット変更
	ld	b,2
	ld	c,2
	ld	d,$06
	ld	a,[f_SelectCommand]
	ld	e,a
	ld	hl,MENUATRMAP_BUFFER + 12
	ld	a,l				;位置をずらす
	add	a,e
	add	a,e
	ld	l,a
	call	g_SetTileFill
	;フィールド画面の描画
	call	bf_Field
	ret